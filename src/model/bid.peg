Rule := level=Digit bid=BidSpecifier ': ' restrictions=RestrictionList $
BidSpecifier := Wildcard | SuitSpecifier

Wildcard := 'x'
SuitSpecifier := Major | Minor | Strain
Major := 'M'
Minor := 'm'

Strain := Suit | Notrump
Suit := Club | Diamond | Heart | Spade
Club := '[Cc♣♧]'
Diamond := '[Dd♦♢]'
Heart := '[Hh♥♡]'
Spade := '[Ss♠♤]'
Notrump := '[Nn]' '[Tt]'?


RestrictionList := RestrictionListItem+
RestrictionListItem := restriction=Restriction ' '?
Restriction := RestrictionGroup | RestrictionOr | SuitRange | SuitBound | PointRange | PointBound | Distribution | Response 
RestrictionOr := left=Restriction {' or ' | ' / '} right=Restriction
RestrictionGroup := '\(' RestrictionList '\)'

PointRange := lower=Number '-' upper=Number
PointBound := value=Number qualifier=BoundQualifier
SuitRange := lower=Digit '-' upper=Digit suit=SuitRangeSpecifier
SuitBound := value=Number qualifier=BoundQualifier suit=SuitRangeSpecifier
SuitRangeSpecifier := Major | Minor | OtherMajor | OtherMinor | Strain
OtherMajor := 'oM'
OtherMinor := 'om'

BoundQualifier := Plus | Minus | Equals
Plus := '\+'
Minus := '\-'
Equals := '=' | ''

Distribution := Balanced | Unbalanced | Shape
Balanced := 'BAL'
Unbalanced := 'unBAL'
Shape := s=Digit h=Digit d=Digit c=Digit
Response := ForceOneRound | ForceGame | ForceSlam | Puppet
ForceOneRound := 'F1'
ForceGame := 'FG'
ForceSlam := 'FS'
Puppet := level=Number strain=Strain

Digit := literal='[0-9]'
  .value = number { return parseInt(literal) }
Number := literal='[0-9]+'
  .value = number { return parseInt(literal) }
