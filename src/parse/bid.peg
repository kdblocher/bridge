Start := bid=Bid $
Bid := level=Digit bid=BidSpecifier ': ' constraints=ConstraintList
BidSpecifier := Wildcard | SuitSpecifier

Wildcard := v='x'
SuitSpecifier := Major | Minor | Strain
Major := v='M'
Minor := v='m'

Strain := Suit | Notrump
Suit := Club | Diamond | Heart | Spade
Club := v='[Cc♣♧]'
Diamond := v='[Dd♦♢]'
Heart := v='[Hh♥♡]'
Spade := v='[Ss♠♤]'
Notrump := v='[Nn]' '[Tt]'?

ConstraintList := ConstraintListItem+
ConstraintListItem := constraint=Constraint ' '?
Constraint :=  ConstraintConst | ConstraintOr | ConstraintAnd | ConstraintNot | Distribution | Response | SuitRange | SuitComparison | SuitBound | PointRange | PointBound

ConstraintConst := ConstraintTrue | ConstraintFalse
ConstraintTrue := v='true'
ConstraintFalse := v='false'

ConstraintOr := left=Constraint ' or ' right=Constraint
ConstraintAnd := '\(' constraints=ConstraintList '\)'
ConstraintNot := {'not' | '!'} ' '? constraint=Constraint


PointRange := lower=Number '-' upper=Number
PointBound := value=Number qualifier=BoundQualifier

SuitRange := lower=Digit '-' upper=Digit suit=SuitRangeSpecifier
SuitBound := value=Number qualifier=BoundQualifier suit=SuitRangeSpecifier
SuitRangeSpecifier := Major | Minor | Suit //| OtherMajor | OtherMinor
OtherMajor := v='oM'
OtherMinor := v='om'

SuitComparison := left=Suit op=SuitComparisonOperator right=Suit
SuitComparisonOperator := v='<' | v='<=' | v='=' | v='>=' | v='>'

BoundQualifier := Plus | Minus | Equals
Plus := v='\+'
Minus := v='\-'
Equals := v='=' | v=''

Distribution := Balanced | SemiBalanced | Unbalanced | AnyShape | SpecificShape
Balanced := v='BAL'
SemiBalanced := v='semiBAL'
Unbalanced := v='unBAL'
AnyShape := v='[0-9]{4}' '\*'
SpecificShape := S=Digit H=Digit D=Digit C=Digit

Response := ForceOneRound | ForceGame | ForceSlam | Relay
ForceOneRound := v='F1'
ForceGame := v='FG'
ForceSlam := v='FS'
Relay := '->' level=Number strain=Strain

Digit := literal='[0-9]'
  .value = number { return parseInt(literal) }
Number := literal='[0-9]+'
  .value = number { return parseInt(literal) }
