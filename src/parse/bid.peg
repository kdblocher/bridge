Start := bid=Bid $
Bid := level=Digit bid=BidSpecifier ': ' constraints=ConstraintList
BidSpecifier := Wildcard | SuitSpecifier

Wildcard := 'x'
SuitSpecifier := Major | Minor | Strain
Major := 'M'
Minor := 'm'

Strain := Suit | Notrump
Suit := Club | Diamond | Heart | Spade
Club := '[Cc♣♧]'
Diamond := '[Dd♦♢]'
Heart := '[Hh♥♡]'
Spade := '[Ss♠♤]'
Notrump := '[Nn]' '[Tt]'?

ConstraintList := ConstraintListItem+
ConstraintListItem := constraint=Constraint ' '?
Constraint := ConstraintAnd | ConstraintOr | Distribution | Response | SuitRange | SuitBound | PointRange | PointBound 
ConstraintOr := left=Constraint {' or ' | ' / '} right=Constraint
ConstraintAnd := '\(' constraints=ConstraintList '\)'

PointRange := lower=Number '-' upper=Number
PointBound := value=Number qualifier=BoundQualifier

SuitRange := lower=Digit '-' upper=Digit suit=SuitRangeSpecifier
SuitBound := value=Number qualifier=BoundQualifier suit=SuitRangeSpecifier
SuitRangeSpecifier := Major | Minor | OtherMajor | OtherMinor | Suit
OtherMajor := v='oM'
OtherMinor := v='om'

BoundQualifier := Plus | Minus | Equals
Plus := v='\+'
Minus := v='\-'
Equals := v='=' | v=''

Distribution := Balanced | SemiBalanced | Unbalanced | Shape
Balanced := v='BAL'
SemiBalanced := v='semiBAL'
Unbalanced := v='unBAL'
Shape := S=Digit H=Digit D=Digit C=Digit
Response := ForceOneRound | ForceGame | ForceSlam | Relay
ForceOneRound := v='F1'
ForceGame := v='FG'
ForceSlam := v='FS'
Relay := level=Number strain=Strain

Digit := literal='[0-9]'
  .value = number { return parseInt(literal) }
Number := literal='[0-9]+'
  .value = number { return parseInt(literal) }
