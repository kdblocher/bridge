Start := spec=BidSpec $
BidSpec := bid=Bid constraints=BidSpecConstraintList?
BidSpecConstraintList := ': ' constraints=ConstraintList
Bid := ContractBid | NonContractBid

ContractBid := level=Digit specifier=StrainSpecifier
StrainSpecifier := Wildcard | Major | Minor | Strain
Wildcard := v='X'
Major := v='M'
Minor := v='m'

NonContractBid := Pass
Pass := v='P'

Strain := Suit | Notrump
Suit := Club | Diamond | Heart | Spade
Club := v='C'
Diamond := v='D'
Heart := v='H'
Spade := v='S'
Notrump := v='N' 'T'?

Honor := v='[AKQJT]'

ConstraintList := ConstraintListItem+
ConstraintListItem := constraint=Constraint ' '?
Constraint :=
    Const
  | Or
  | And
  | Not
  | Otherwise
  | Distribution
  // | Response
  | SuitRange
  | SuitBound
  | SuitComparison
  | SuitHonors
  | SuitTop
  | SuitRank
  | SetTrump
  | PointRange
  | PointBound
  | OtherBid
  | LabelDef
  | LabelRef

Const := True | False
True := v='true'
False := v='false'

Or := left=Constraint ' or ' right=Constraint
And := '\(' constraints=ConstraintList '\)'
Not := {'not' | '!'} ' '? constraint=Constraint
Otherwise := v='else'

PointRange := lower=Number '-' upper=Number
PointBound := value=Number qualifier=BoundQualifier

SuitRange := lower=Digit '-' upper=Digit suit=SuitSpecifier
SuitBound := value=Number qualifier=BoundQualifier suit=SuitSpecifier
SuitSpecifier := Wildcard | Major | Minor | OtherMajor | OtherMinor | Suit
OtherMajor := v='oM'
OtherMinor := v='om'

SuitComparison := left=SuitSpecifier op=SuitComparisonOperator right=SuitSpecifier
SuitComparisonOperator := v='<=' | v='>=' | v='=' | v='<' | v='>'

SuitHonors := suit=SuitSpecifier honors=Honor+
SuitTop := suit=SuitSpecifier x='[0-5]' '/' y='[1-5]'

BoundQualifier := Plus | Minus | Equals
Plus := v='\+'
Minus := v='\-'
Equals := v='='

OtherBid := level=Number strain=Strain

Distribution := Balanced | SemiBalanced | Unbalanced | AnyShape | SpecificShape
Balanced := v='BAL'
SemiBalanced := v='semiBAL'
Unbalanced := v='unBAL'
AnyShape := v='[0-9]{4}' '\*'
SpecificShape := S=Digit H=Digit D=Digit C=Digit

SuitRank := Primary | Secondary
Primary := suit=SuitSpecifier '1'
Secondary := suit=SuitSpecifier '2'

SetTrump := suit=SuitSpecifier '#'

// Response := ForceOneRound | ForceGame | ForceSlam | Relay
// ForceOneRound := v='F1'
// ForceGame := v='FG'
// ForceSlam := v='FS'
// Relay := '->' bid=OtherBid


LabelDef := '\'' label=Label '\': ' constraints=ConstraintList
LabelRef := '\'' label=Label '\''
Label := v='[0-9a-zA-z-_]+'

Digit := literal='[0-9]'
  .value = number { return parseInt(literal) }
Number := literal='[0-9]+'
  .value = number { return parseInt(literal) }