Start := spec=BidSpec $
BidSpec := bid=Bid constraints=BidSpecConstraintList?
BidSpecConstraintList := ': ' constraints=ConstraintList
Bid := ContractBid | NonContractBid

ContractBid := level=Digit specifier=SuitSpecifier
SuitSpecifier := Wildcard | Major | Minor | Strain
Wildcard := v='x'
Major := v='M'
Minor := v='m'

NonContractBid := Pass
Pass := v='P'

Strain := Suit | Notrump
Suit := Club | Diamond | Heart | Spade
Club := v='[Cc♣♧]'
Diamond := v='[Dd♦♢]'
Heart := v='[Hh♥♡]'
Spade := v='[Ss♠♤]'
Notrump := v='[Nn]' '[Tt]'?

Honor := v='[AKQJT]'

ConstraintList := ConstraintListItem+
ConstraintListItem := constraint=Constraint ' '?
Constraint :=
    Const
  | Or
  | And
  | Not
  | Otherwise
  | Distribution
  | Response
  | SuitRange
  | SuitBound
  | SuitComparison
  | SuitHonors
  | SuitTop
  | SuitRank
  | PointRange
  | PointBound
  | OtherBid
  | LabelDef
  | LabelRef

Const := True | False
True := v='true'
False := v='false'

Or := left=Constraint ' or ' right=Constraint
And := '\(' constraints=ConstraintList '\)'
Not := {'not' | '!'} ' '? constraint=Constraint
Otherwise := v='else'

PointRange := lower=Number '-' upper=Number
PointBound := value=Number qualifier=BoundQualifier

SuitRange := lower=Digit '-' upper=Digit suit=SuitRangeSpecifier
SuitBound := value=Number qualifier=BoundQualifier suit=SuitRangeSpecifier
SuitRangeSpecifier := SuitSpecifier //| Major | Minor | OtherMajor | OtherMinor
// OtherMajor := v='oM'
// OtherMinor := v='om'

SuitComparison := left=Suit op=SuitComparisonOperator right=Suit
SuitComparisonOperator := v='<' | v='<=' | v='=' | v='>=' | v='>'

SuitHonors := suit=Suit honors=Honor+
SuitTop := suit=Suit x='[0-5]' '/' y='[1-5]'

BoundQualifier := Plus | Minus | Equals
Plus := v='\+'
Minus := v='\-'
Equals := v='='

OtherBid := level=Number strain=Strain

Distribution := Balanced | SemiBalanced | Unbalanced | AnyShape | SpecificShape
Balanced := v='BAL'
SemiBalanced := v='semiBAL'
Unbalanced := v='unBAL'
AnyShape := v='[0-9]{4}' '\*'
SpecificShape := S=Digit H=Digit D=Digit C=Digit

SuitRank := Primary | Secondary
Primary := suit=Suit '1'
Secondary := suit=Suit '2'

Response := ForceOneRound | ForceGame | ForceSlam | Relay
ForceOneRound := v='F1'
ForceGame := v='FG'
ForceSlam := v='FS'
Relay := '->' bid=OtherBid

LabelDef := '\'' label=Label '\': ' constraints=ConstraintList
LabelRef := '\'' label=Label '\''
Label := v='[0-9a-zA-z-_]+'

Digit := literal='[0-9]'
  .value = number { return parseInt(literal) }
Number := literal='[0-9]+'
  .value = number { return parseInt(literal) }